apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "io.github.emanual.app"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 17
        versionName "2.3.2"

    }
    signingConfigs {
        //这里是读取`app/local.properties`文件，用于提取keystroe_storeFile等key的值
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        releaseConfig {
            // relase签名
            storeFile file(properties.getProperty("keystroe_storeFile"))
            storePassword properties.getProperty("keystroe_storePassword")
            keyAlias properties.getProperty("keystroe_keyAlias")
            keyPassword properties.getProperty("keystroe_keyPassword")
        }
    }
    //多渠道
    productFlavors {
        xiaomi {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
        }
        _360 {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "_360"]
        }
        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
        meizu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "meizu"]
        }
        iemanual {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "iemanual.com"]
        }
    }

    buildTypes {


        debug{
            signingConfig signingConfigs.releaseConfig
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"

        }
        release {
            signingConfig signingConfigs.releaseConfig

            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled false
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //learn cloud 为了解决部分第三方库重复打包了META-INF的问题
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }


}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile "com.android.support:design:23.1.1"
    compile "com.android.support:support-v4:23.1.1"
    compile "com.android.support:recyclerview-v7:23.1.1"
    compile "com.android.support:cardview-v7:23.1.1"

    //https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.beardedhen:androidbootstrap:2.0.1'
    //https://github.com/jpardogo/PagerSlidingTabStrip
    compile 'com.jpardogo.materialtabstrip:library:1.1.0'
    //https://github.com/loopj/android-async-http
    compile 'com.loopj.android:android-async-http:1.4.9'

    //avoscloud-sdk 为 LeanCloud基础包 https://leancloud.cn/docs/sdk_down.html
    compile 'cn.leancloud.android:avoscloud-sdk:v3.12.1'
    compile project(':extras:umeng-update')
}
